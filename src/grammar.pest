Program  = _{ SOI ~ ExprList ~ EOI }
ExprList =  { (Expr ~ ";")* ~ Expr }
Expr     = _{ BinaryExpr | Let | Function | Term | If }

// Int (Int -> Int)
Int  = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
Bool = @{ "True" | "False" }

Term = _{ Int | Bool | Var | Function | "(" ~ Expr ~ ")" }

BinaryExpr   =  { Term ~ Operator ~ Term }
Operator     =  { IntOperator | CompOperator | BoolOperator }
IntOperator  = _{ "+" | "-" | "*" | "/" }
CompOperator = _{ "<=" | "<" | ">=" | ">" | "==" | "!=" }
BoolOperator = _{ "&&" | "||" }

// If
If = { "if" ~ "(" ~ Expr ~ ")" ~ "{" ~ ExprList ~ "}" ~ "else" ~ "{" ~ ExprList ~ "}" }

// Let
Let = { "let" ~ Var ~ "=" ~ (Function | Term) }
Function = { "(" ~ Var ~ ")" ~ "=>" ~ "{" ~ ExprList ~ "}" }
Var = { !ForbiddenTerms ~ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

ForbiddenTerms = { "if" | "let" | "True" | "False" }

WHITESPACE = _{ " " | "\t" | NEWLINE }